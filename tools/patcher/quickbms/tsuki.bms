# Tsukihime (Remake)
# HED/MRG/NAM extract
# QuickBMS script by DKDave, 2021

# Notes:
# Use this script on the .HED file


Open FDDE "mrg" 1
Open FDDE "nam" 2

Get ENTRIES asize
Math ENTRIES / 8

Math HED_ENTRY = 0
Math NAM_ENTRY = 0

For A = 0 < ENTRIES
	Goto HED_ENTRY
	Get OFFSET Long

	If OFFSET = 0xFFFFFFFF
		Break
	Endif

	Get SIZE Short
	Math OFFSET * 0x800
	Math SIZE * 0x800

	Goto NAM_ENTRY 2
	Get FILENAME String 2

	If SIZE <> 0
		Goto OFFSET 1
		GetDString FILE_TYPE 4 1

		If FILE_TYPE = "NXCX"
			Get SIZE Long 1
			Get ZSIZE Long 1
			Get JUNK Long 1
			SavePos OFFSET2 1

			Comtype zlib
			CLog FILENAME OFFSET2 ZSIZE SIZE 1

		Elif FILE_TYPE = "NXGX"
			Get SIZE Long 1
			Get ZSIZE Long 1
			Get JUNK Long 1
			SavePos OFFSET2 1

			Comtype gzip
			CLog FILENAME OFFSET2 ZSIZE SIZE 1
        Else
            Log FILENAME OFFSET SIZE 1
		Endif

	Endif

	Math HED_ENTRY + 8
	Math NAM_ENTRY + 0x20
Next A

